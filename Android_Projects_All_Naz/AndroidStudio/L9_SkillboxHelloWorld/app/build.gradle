plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "com.skillbox.skillboxhelloworld"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "DebugSkillbox"]
        }

        qa {
            initWith debug
            manifestPlaceholders = [appName: "QaSkillbox"]
            applicationIdSuffix ".qa"
        }

        release {
            manifestPlaceholders = [appName: "ReleaseSkillbox"]
            applicationIdSuffix ".release"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "type", "brand"

    productFlavors {
        demo {
            applicationIdSuffix ".demo"
            versionNameSuffix "Demo"
            dimension "type"
        }

        full {
            applicationIdSuffix ".full"
            versionNameSuffix "Full"
            dimension "type"
        }

        cola {
            versionCode = android.defaultConfig.versionCode + 100
            applicationIdSuffix ".cola"
            versionNameSuffix "Cola"
            dimension "brand"
        }

        pepsi {
            minSdkVersion 29
            applicationIdSuffix ".pepsi"
            versionNameSuffix "Pepsi"
            dimension "brand"
        }
    }

    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}